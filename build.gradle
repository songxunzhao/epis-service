buildscript {
    ext {
		springBootVersion = '2.1.4.RELEASE'
		awsSdkVersion = '1.11.390'
		springBootAdminVersion = '2.1.3'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		maven {
			url "http://dl.bintray.com/nilsmagnus/maven"
		}
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0")
		classpath("no.nils:wsdl2java:0.10")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'no.nils.wsdl2java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'com.gorylenko.gradle-git-properties'

jar {
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven {
		url "https://pubnexus.zerotech.ee/content/repositories/zeropublic/"
	}
}

processResources {
	from ('.ebextensions/') {
		into '.ebextensions'
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
		mavenBom "com.amazonaws:aws-java-sdk-bom:${awsSdkVersion}"
	}
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-amqp")
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-web-services')
	implementation("de.codecentric:spring-boot-admin-starter-client")
	runtimeOnly("org.springframework.boot:spring-boot-devtools")
	runtimeOnly('org.postgresql:postgresql')
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.3.RELEASE")
	compileOnly('org.projectlombok:lombok:1.18.6')
	annotationProcessor('org.projectlombok:lombok:1.18.6')
	implementation("org.jsoup:jsoup:1.8.1")
	implementation('org.apache.commons:commons-lang3:3.5')
	implementation('com.tapstream:rollbar-logback:0.1.4')

	implementation('com.getsentry.raven:raven-logback:7.8.3')
	implementation('io.springfox:springfox-swagger2:2.6.1')
	implementation('io.springfox:springfox-swagger-ui:2.6.1')

	implementation('com.amazonaws:aws-java-sdk-ssm')

	implementation('org.hibernate:hibernate-java8')
	implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	implementation('com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.8.10')

	implementation('com.rollbar:rollbar:0.5.3')
	implementation('com.mandrillapp.wrapper.lutung:lutung:0.0.7')

	implementation('org.springframework:spring-jms')
	implementation fileTree(dir: 'lib', include: '*.jar')

	implementation('ee.tuleva:epis-api:1.0') //TODO: remove
	implementation 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:0.11.0'

	// enable extension support for wsdl2java
	wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:0.11.0'
	wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.0'

	testImplementation("com.h2database:h2")
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude module: "spock-core"
	}
	testImplementation("org.spockframework:spock-core:1.3-groovy-2.4")
	testImplementation("org.spockframework:spock-spring:1.3-groovy-2.4")
	testImplementation("org.springframework.security:spring-security-test")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
}

dependencyManagement {
	imports {
		mavenBom("de.codecentric:spring-boot-admin-dependencies:$springBootAdminVersion")
	}
}


test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
		showCauses true
		showExceptions true
		showStackTraces true
		exceptionFormat = 'full'
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDirs file('src/integration-test/java')
		}

		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDirs file('src/integration-test/groovy')
		}

		resources.srcDirs file('src/integration-test/resources')
	}
}

task integrationTest(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	enableAssertions = false
}

bootRun {

//	systemProperties['javax.net.debug'] = 'ssl'

//	systemProperties['com.sun.net.ssl.checkRevocation'] = 'false'

	systemProperties['spring.profiles.active'] = 'dev'
}

wsdl2java {
	wsdlsToGenerate = [
			['-xjc-Xequals', '-xjc-XhashCode', '-xjc-XtoString', 'src/main/resources/epis-wsdl/epis.wsdl']
	]
	wsdlDir = file("src/main/resources/epis-wsdl")
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		html.enabled false
	}
}

check.dependsOn jacocoTestReport
